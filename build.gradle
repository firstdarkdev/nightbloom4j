plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '8.0.0'
}

group = 'com.hypherionmc.nightbloom'
version = '1.0.0'

configurations {
    shadeMe
    implementation.extendsFrom shadeMe
}

repositories {
    mavenCentral()
}

dependencies {
    shadeMe("com.google.code.gson:gson:2.10.1")
    shadeMe("com.squareup.okhttp3:okhttp:4.12.0")

    compileOnly("org.projectlombok:lombok:1.18.30")
    annotationProcessor("org.projectlombok:lombok:1.18.30")

    testCompileOnly("org.projectlombok:lombok:1.18.30")
    testAnnotationProcessor("org.projectlombok:lombok:1.18.30")

    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

shadowJar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    configurations = [project.configurations.shadeMe]
    archiveClassifier.set(null)
}

jar.finalizedBy(shadowJar)

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact shadowJar
        }
    }

    repositories {
        maven {
            url System.getenv('MAVEN_URL')
            credentials {
                username System.getenv('MAVEN_USER')
                password System.getenv('MAVEN_PASS')
            }
        }
    }
}